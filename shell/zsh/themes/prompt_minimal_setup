#!/usr/bin/env zsh

DIRTY="✗"
NONE="✓"

prompt_minimal_pwd() {
  prompt_short_dir=$($DOTFILES_PATH/shell/scripts/filesystem/short_pwd)
  print -n "%F{yellow}${prompt_short_dir}"
}

prompt_minimal_git() {
  [[ -n ${git_info} ]] && print -n "%F{white} on${(e)git_info[prompt]}"
}

prompt_minimal_precmd() {
  (( ${+functions[git-info]} )) && git-info
}

prompt_minimal_setup() {
  local prompt_minimal_status='%(?:%F{green}ρ:%F{red}ρ)'

  autoload -Uz add-zsh-hook && add-zsh-hook precmd prompt_minimal_precmd

  prompt_opts=(cr percent sp subst)

  zstyle ':zim:git-info:branch' format '%F{green}%b'
  zstyle ':zim:git-info:commit' format '%c'
  zstyle ':zim:git-info:clean' format '%F{white}$NONE'
  zstyle ':zim:git-info:dirty' format '%F{yellow}$DIRTY'
  zstyle ':zim:git-info:keys' format 'prompt' ' %F{cyan}%b%c %C%D'

  PS1="${prompt_minimal_status} \$(prompt_minimal_pwd)\$(prompt_minimal_git)%f "
  RPS1=''
}

prompt_minimal_setup "${@}"
